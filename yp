#!/usr/bin/perl -w -I.

# ===========================================================================
# File:		yp
# Purpose:	Generator for a Yawns-Planet website.
# Created:	2005-10-12
#
# ===========================================================================
# (c) 2005 Steve Kemp <steve@steve.org.uk>
# This program is free software; you can redistribute it and/or modify
# it under the terms of version 2 of the GNU General Public License as
# published by the Free Software Foundation.  See the file COPYING for
# further details.
# ===========================================================================
#
# $Id: yp,v 1.25 2005-10-14 18:17:16 steve Exp $

# Enforce good programming practices
use strict;

# Modules we use.
use Date::Manip qw(ParseDate UnixDate);
use HTML::Entities;
use HTML::Template;
use POSIX qw/ strftime /;


# Custom modules
use conf::SiteConfig;
use YawnsBlog;



#
# Read-only variables: version number from CVS.
#
my $REVISION  = '$Id: yp,v 1.25 2005-10-14 18:17:16 steve Exp $';
my $VERSION   = "";
$VERSION      = join (' ', (split (' ', $REVISION))[2..2]);
$VERSION      =~ s/yp,v\b//;


#
# 0. Find the directory to load the tempaltes from.
#
my $TEMPLATES = get_conf( "template_dir" );
testTemplateDirectory( $TEMPLATES );



#  1. Get weblog entries.
#
my @entries = YawnsBlog::Entries( get_conf( "count" ) );

#
#  2. Create output HTML
#
writeHTMLIndex( @entries );


#
#  3. Create output XML feeds.
#
writeRSS10( @entries );
writeRSS20( @entries );


#
#  4.  All done.
#
exit;



#
#  Ensure the given templates directory exists, and that it
# contains the templates that we will expect to load from it.
#
sub testTemplateDirectory
{
    my ( $directory ) = ( @_ );

    if ( ! -d $TEMPLATES )
    {
	print <<E_O_ERROR;

The templates directory you have chosen doesn't appear to exist:

  $TEMPLATES

Please update the following configuration file to correct this:

  'conf/SiteConfig.pm'

Exiting.
E_O_ERROR
	exit;
    }

    foreach my $file ( qw( index.tmpl rss10.tmpl rss20.tmpl ) )
    {
	if ( ! -e $directory . "/" . $file )
	{
	    print "Required template file '$file' not found in templates " .
	          "directory '$directory'\n";
	    exit;
	}
    }
}



#
#  Write the output index.html file
#
sub writeHTMLIndex
{
    my ( @entries ) = ( @_ );

    #
    # Load the template.
    #
    my $template = HTML::Template->new( filename =>  $TEMPLATES . "index.tmpl" );

    #
    # Add the blogs and subscription information.
    #
    $template->param( 'blogs', @entries );
    $template->param( 'subscriptions', YawnsBlog::Posters() );

    my $time = strftime ("%e %B %Y - %H:%M:%S", localtime);

    #
    # Setup static parameters.
    #
    $template->param( 'title',      get_conf( 'title' ) );
    $template->param( 'title_link', get_conf( 'title_link' ) );
    $template->param( 'updated' ,   $time );
    $template->param( 'version' ,   $VERSION );


    #
    # Open the output file, and write the data.
    #
    my $output = get_conf( "htmlOutput" );

    open( OUTPUT, ">", $output )
	or die "Cannot open output file '$output' - $!@";
    print OUTPUT $template->output();
    close( OUTPUT );
}



#
#  Write out an RSS v1.0 formatted XML file from the weblog
# entries.
#
#  We do this cleanly setting each template loop's value
# correctly - taking care to modify the date to the appropriate
# format.
#
sub writeRSS10
{
    my ( $weblogs ) = ( @_ );

    my $contents;
    my $entries;

    #
    # Read in each available blog entry.  We don't care about
    # much of the date just:
    #
    # Username, ID, title, body, and date.
    #
    foreach my $entry ( @$weblogs )
    {
	my $title = $entry->{'title'};
	my $user  = $entry->{'user'};
	my $id    = $entry->{'id'};
	my $body  = $entry->{'body'};
	my $date  = $entry->{'date'};
	my $time  = $entry->{'time'};


	#
	#  Make sure the date is in the right format for
	# the RSS v1.0 feed.
	#
	# e.g.
	#      2005-10-11T14:39:00+00:00
	#
	my $dp  = ParseDate( $time . " " . $date );
	$date = UnixDate( $dp, "%Y-%m-%dT%H:%M:00+00:00");

	#
	#  HTML Encode the title, and body, so that it is valid XML
	#
	$title = encode_entities( $title );
	$body  = encode_entities( $body );

	push( @$contents,
	      {
		  username => $user,
		  id       => $id,
	      } );

	push( @$entries,
	      {
		  username => $user,
		  id       => $id,
		  title    => $title,
		  bodytext => $body,
		  date     => $date
		  } );

    }

    #
    # Load the template.
    #
    my $template = HTML::Template->new( filename =>  $TEMPLATES . "rss10.tmpl" );

    #
    #  Set the parameters
    #
    $template->param( "contents", $contents );
    $template->param( "blogentries", $entries );

    #
    # Open the output file, and write the data.
    #
    my $output = get_conf( "rss1Output" );
    open( RSS10OUTPUT, ">", $output )
	or die "Cannot open output file '$output' - $!@";
    print RSS10OUTPUT $template->output();
    close( RSS10OUTPUT );
}



#
#  Write out an RSS v2.0 formatted XML file from the weblog
# entries.
#
#  We do this cleanly setting each template loop's value
# correctly - taking care to modify the date to the appropriate
# format.
#
sub writeRSS20
{
    my ( $weblogs ) = ( @_ );

    my $entries;

    #
    # Read in each available blog entry.  We don't care about
    # much of the date just:
    #
    # Username, ID, title, body, and date.
    #
    foreach my $entry ( @$weblogs )
    {
	my $title = $entry->{'title'};
	my $user  = $entry->{'user'};
	my $id    = $entry->{'id'};
	my $body  = $entry->{'body'};
	my $date  = $entry->{'date'};
	my $time  = $entry->{'time'};


	#
	#  Make sure the date is in the right format for
	# the RSS v2.0 feed.
	#
	# e.g.
	#      Sat, 10 Sep 2005 02:44:00 +0000
	#
	my $dp  = ParseDate( $time . " " . $date );
	$date = UnixDate( $dp, "%a, %d %b %Y %H:%M:%S +0000" );

	#
	#  HTML Encode the title, and body, so that it is valid XML
	#
	$title = encode_entities( $title );
	$body  = encode_entities( $body );

	push( @$entries,
	      {
		  username => $user,
		  id       => $id,
		  title    => $title,
		  bodytext => $body,
		  date     => $date
		  } );

    }

    #
    # Load the template.
    #
    my $template = HTML::Template->new( filename => $TEMPLATES . "rss20.tmpl" );


    #
    #  Setup the entries.
    #
    $template->param( "entries", $entries );

    #
    # Open the output file, and write the data.
    #
    my $output = get_conf( "rss2Output" );
    open( OUTPUT, ">", $output )
	or die "Cannot open output file '$output' - $!@";
    print OUTPUT $template->output();
    close( OUTPUT );
}
